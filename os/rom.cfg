# Memory map for buri 6502 computer
MEMORY
{
    ZEROPAGE:   start=$0000, size=$0100, define=yes;            # Zero page
    STACK:      start=$0100, size=$0100, define=yes;            # Stack page
    OSDATA:     start=$0200, size=$0100, define=yes;            # OS workspace
    OSCSTACK:   start=$0300, size=$0100, define=yes;            # OS C call stack
    SCREEN:     start=$0400, size=$0800, define=yes;            # screen buffer
    USR:        start=$0C00, size=$7400, define=yes;            # User memory
    IO:         start=$DF80, size=$0080, define=yes;            # 8x16 byte IO pages
    ROM:        start=$E000, size=$2000, file="%O";             # 8K of ROM

    # Well known IO devices
    NS16450_A:  start=$D000, size=$0008, define=yes;
    CRTC6845:   start=$D010, size=$0002, define=yes;
}

# Segments within the EEPROM
SEGMENTS
{
    # Initialised segments
    DATA:       load=ROM, run=OSDATA, type=rw, define=yes;      # OS read/write data
    RODATA:     load=ROM, type=ro;                              # Read-only data segment
    CODE:       load=ROM, type=ro;                              # ROM code
    VECTORS:    load=ROM, type=ro, start=$FFF0;                 # Processor vector table

    # Uninitialised segments
    BSS:        load=OSDATA, type=bss;                          # OS temp storage
    ZEROPAGE:   load=ZEROPAGE, type=zp;                         # Zero-page
    OSZP:       load=ZEROPAGE, type=zp, start=$D0;              # OS-section of zero page
}
